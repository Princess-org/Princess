import vector
import map
import set
import toolchain
import json

import server
import cache

export def setup_file_loader {
    toolchain::file_loader = *load_file
}

export let open_documents = set::make()

export def load_file(filename: String) -> String {
    let module = toolchain::find_module_by_path(filename)
    if module {
        return module.text
    }
    let text = toolchain::load_file(filename)
    return text
}

export def open_document(uri: server::Str, text: Str) {
    open_documents.add(uri)
    let path = uri_to_path(uri)
    cache::recompile(path)
}

export def save_document(uri: server::Str, text: Str) {
    open_document(uri, text)
}

// TODO This is very inefficient, change the lexer to accept split lines as well instead
export def find_offset(text: Str, pos: server::U16Position) -> size_t {
    var u8_offset = 0
    var line = 0
    var u16_column = 0
    while u8_offset < text.length() {
        if line == pos.line and u16_column == pos.character {
            return u8_offset
        }
        let c = text[u8_offset]
        if c == '\r' {
            if u8_offset + 1 < text.length() {
                if text[u8_offset + 1] == '\n' {
                    u8_offset += 1
                }
            }
            u16_column = 0
            line += 1
            u8_offset += 1
        } else if c == '\n' {
            u16_column = 0
            line += 1
            u8_offset += 1
        } else {
            if c & 0xF0 == 0xC0 or c & 0xF0 == 0xD0 {
                u8_offset += 2
                u16_column += 1
            } else if c & 0xF0 == 0xE0 {
                u8_offset += 3
                u16_column += 2
            } else if c & 0xF0 == 0xF0 {
                u8_offset += 4
                u16_column += 2
            } else {
                u8_offset += 1
                u16_column += 1
            }
        }        
    }
    return u8_offset
}

export def u16_pos_to_u8(filename: Str, pos: server::U16Position) -> server::U8Position {
    let module = toolchain::find_module_by_path(filename)
    if not module or pos.line < 0 or pos.character < 0 { 
        return { pos.line, pos.character } !server::U8Position 
    }

    let line = module.lines[pos.line]
    var u8_column = 0
    var u16_column = 0
    while u8_column < line.length {
        if u16_column == pos.character { break }

        let c = line[u8_column]
        if c & 0xF0 == 0xC0 or c & 0xF0 == 0xD0 {
            u8_column += 2
            u16_column += 1
        } else if c & 0xF0 == 0xE0 {
            u8_column += 3
            u16_column += 2
        } else if c & 0xF0 == 0xF0 {
            u8_column += 4
            u16_column += 2
        } else {
            u8_column += 1
            u16_column += 1
        }
    }
    return { pos.line, u8_column } !server::U8Position 
} 

export def u8_pos_to_u16(filename: Str, pos: server::U8Position) -> server::U16Position {
    let module = toolchain::find_module_by_path(filename)
    if not module or pos.line < 0 or pos.character < 0 { 
        return { pos.line, pos.character } !server::U16Position 
    }

    let line = module.lines[pos.line]
    var u8_column = 0
    var u16_column = 0
    while u8_column < line.length {
        if u8_column == pos.character { break }

        let c = line[u8_column]
        if c & 0xF0 == 0xC0 or c & 0xF0 == 0xD0 {
            u8_column += 2
            u16_column += 1
        } else if c & 0xF0 == 0xE0 {
            u8_column += 3
            u16_column += 2
        } else if c & 0xF0 == 0xF0 {
            u8_column += 4
            u16_column += 2
        } else {
            u8_column += 1
            u16_column += 1
        }
    }
    return { pos.line, u16_column } !server::U16Position 
}

def count_new_lines(text: Str) -> int {
    var new_lines = 0
    for var i in 0..text.length {
        if text[i] == '\n' {
            new_lines += 1
        }
    }
    return new_lines
}

def get_last_line_length(text: Str) -> size_t {
    let last_nl = last_index_of(text, "\n")
    if last_nl < 0 { return text.length }
    else {
        return text.length - last_nl - 1
    }
}

export def change_document(uri: server::Str, changes: &[server::TextDocumentContentChangeEvent]) {
    let path = uri_to_path(uri)
    let module = toolchain::find_module_by_path(path)
    let text = module.text

    let recompile_changes = vector::make(server::TextDocumentChangeEvent)

    for var i in 0..changes.size {
        let change = changes[i]
        if change.range {
            // TODO this is ugly, allow optionals instead
            let range = json::deserialize(change.range, server::Range).get()
            let start_offset = find_offset(module.text, range.start)
            let end_offset = find_offset(module.text, range.end)

            if start_offset != end_offset {
                module.text = module.text.remove(
                    min(text.length() - 1, start_offset) !size_t, 
                    end_offset - start_offset
                )
            }
            if start_offset < text.length() {
                module.text = module.text.insert(start_offset, change.text)
            } else {
                module.text = module.text + change.text
            }

            let end = u16_pos_to_u8(path, range.end)
            let start = u16_pos_to_u8(path, range.start)
            cache::offset_errors(path, start, end, change.text) // TODO This could also use the event down below

            let last_line_length = get_last_line_length(change.text)
            let line_count = count_new_lines(change.text)

            let event = {
                end = end,
                lines = start.line - end.line + line_count,
                characters = (last_line_length - end.character 
                    if line_count > 0 
                    else last_line_length + start.character - end.character) !int
            } !server::TextDocumentChangeEvent
            //error(start, " ", end, " ", last_line_length, " ", event.characters, "\n")

            recompile_changes.push(event)

        } else {
            module.text = change.text
        }
    }

    cache::recompile(path, recompile_changes.to_array())
}

export def close_document(uri: server::Str) {
    set::remove(open_documents, uri)
}

// File system events
export def on_created(uri: server::Str) {
    let path = uri_to_path(uri)
    if cache::check_imports(path) {
        cache::recompile(path)
    }
}

export def on_changed(uri: server::Str) {
    if set::contains(open_documents, uri) { return }
    let path = uri_to_path(uri)
    if toolchain::find_module_by_path(path) {
        cache::recompile(path)
    }
}

export def on_deleted(uri: server::Str) {
    let path = uri_to_path(uri)
    cache::check_imports(path)
    let module = toolchain::find_module_by_path(path)
    if module {
        // We don't actually remove the module because that would wreak havoc
        // on the idents that are still existing
        module.is_deleted = true
    }
}